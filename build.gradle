buildscript {
  ext {
    nexus_url = "${project.findProperty('nexus_url') ?: System.getenv('NEXUS_HOST')}"
    nexus_user = "${project.findProperty('nexus_user') ?: System.getenv('NEXUS_USERNAME')}"
    nexus_pw = "${project.findProperty('nexus_pw') ?: System.getenv('NEXUS_PASSWORD')}"
    no_nexus = (project.findProperty('no_nexus') ?: System.getenv('NO_NEXUS') ?: false).toBoolean()
    if (!no_nexus && (nexus_url == "null" || nexus_user == "null" || nexus_pw == "null")) {
      throw new GradleException("property no_nexus='false' (or not defined) but at least one of the properties nexus_url, nexus_user or nexus_pw is not configured. Please configure those properties!")
    }

    def folderRel = (String)("${project.findProperty('nexus_folder_releases') ?: System.getenv('NEXUS_FOLDER_RELEASES')}")
    nexusFolderReleases = folderRel == "null" ? "maven-releases" : folderRel

    def folderSnaps = (String)("${project.findProperty('nexus_folder_snapshots') ?: System.getenv('NEXUS_FOLDER_SNAPSHOTS')}")
    nexusFolderSnapshots = folderSnaps == "null" ? "maven-snapshots" : folderSnaps

    snippetsDir = file('build/generated-snippets')
  }

  repositories {
    if (no_nexus) {
      println("using repositories 'jcenter' and 'mavenCentral', because property no_nexus=$no_nexus")
      jcenter()
      mavenCentral()
    } else {
      println("using nexus repositories")
      maven() {
        url "${nexus_url}/repository/jcenter/"
        credentials {
          username = "${nexus_user}"
          password = "${nexus_pw}"
        }
      }

      maven() {
        url "${nexus_url}/repository/maven-public/"
        credentials {
          username = "${nexus_user}"
          password = "${nexus_pw}"
        }
      }

      maven() {
        url "${nexus_url}/repository/atlassian_public/"
        credentials {
          username = "${nexus_user}"
          password = "${nexus_pw}"
        }
      }
    }
  }
}

buildscript {
  ext {
    joobyVersion = "1.6.6"
  }

  dependencies {
    classpath "org.jooby:jooby-gradle-plugin:$joobyVersion"
  }
}

plugins {
  id "com.github.johnrengelman.shadow" version "5.1.0"
  id "com.google.osdetector" version "1.6.2"
  id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

apply plugin: "application"
apply plugin: "distribution"
apply plugin: "groovy"
apply plugin: "jooby"
apply plugin: "jacoco"

repositories {
  if (no_nexus) {
    println("using repositories 'jcenter' and 'mavenCentral', because property no_nexus=$no_nexus")
    jcenter()
    mavenCentral()
  } else {
    println("using nexus repositories")
    maven() {
      url "${nexus_url}/repository/jcenter/"
      credentials {
        username = "${nexus_user}"
        password = "${nexus_pw}"
      }
    }

    maven() {
      url "${nexus_url}/repository/maven-public/"
      credentials {
        username = "${nexus_user}"
        password = "${nexus_pw}"
      }
    }

    maven() {
      url "${nexus_url}/repository/atlassian_public/"
      credentials {
        username = "${nexus_user}"
        password = "${nexus_pw}"
      }
    }
  }
}

group = 'org.ods'
version = '0.1'
mainClassName = "app.App"
sourceCompatibility = 1.8

dependencyManagement {
  imports {
    mavenBom "org.jooby:jooby-bom:$joobyVersion"
  }
}

dependencies {
  compile "com.github.ben-manes.caffeine:caffeine:2.7.0"
  compile "com.github.jknack:handlebars:4.1.2"
  compile "commons-io:commons-io:2.6"
  compile "io.github.openfeign:feign-core:10.2.3"
  compile "io.github.openfeign:feign-gson:10.2.3"
  compile "net.lingala.zip4j:zip4j:1.3.3"
  compile "org.apache.httpcomponents:httpclient:4.5.8"
  compile "org.codehaus.groovy:groovy-all:2.5.7"
  compile "org.jooby:jooby-jackson"
  compile "org.jooby:jooby-netty"

  compile "io.netty:netty-transport-native-epoll:${dependencyManagement.importedProperties['netty.version']}:${osdetector.classifier.contains('linux') ? 'linux-x86_64' : ''}"
  compile "io.netty:netty-tcnative-boringssl-static:${dependencyManagement.importedProperties['boringssl.version']}" 

  testCompile "junit:junit:4.12"
  testCompile "com.github.stefanbirkner:system-rules:1.19.0" // for managing environment variables
  testCompile "com.github.tomakehurst:wiremock:2.23.2"       // for mocking HTTP server reponses
  testCompile "io.rest-assured:rest-assured:4.0.0"           // for validating REST services
  testCompile "org.spockframework:spock-core:1.3-groovy-2.5"
}

test {
  outputs.dir snippetsDir

  // Use overrides in conf/application.test.conf in ConfigFactory.load()
  // TODO: setting application.env should be enough, but apparently isn't
  systemProperty "application.env", "test"
  systemProperty "config.resource", "application.test.conf"

  testLogging {
    showStandardStreams = true
  }

  finalizedBy jacocoTestReport
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

/** We diverge from the default resources structure to adopt the Jooby standard: */
sourceSets.main.resources {
  srcDirs = ["conf", "public"]
}

/*
bootJar {
  archiveName    "app.jar"
  destinationDir  file("$buildDir/../docker")
}

uploadArchives {
    repositories{
        mavenDeployer {
            repository(url: "${nexus_url}/repository/${nexus_folder}/") {
                 authentication(userName: "${nexus_user}", password: "${nexus_pw}")
            }
            pom.artifactId = 'doc-gen'
            pom.groupId = 'com.bix-digital.pltfmdev'
            pom.version="${System.getenv("TAGVERSION")}" // we will get a TAGVERSION from environment
        }
    }
}
*/
